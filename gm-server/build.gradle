buildscript {
  ext {
    springBootVersion = '1.3.3.RELEASE'
    kotlinVersion = '1.0.1'
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
  }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'kotlin'
apply plugin: 'spring-boot'

repositories {
  mavenCentral()
  maven { url 'http://repo.spring.io/snapshot' }
  maven { url 'https://dl.bintray.com/sdeleuze/maven/' }
}

jar {
  baseName = 'geospatial-messenger'
  version = '1.0.0-SNAPSHOT'
  dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

ext['snippetsDir'] = file('build/generated-snippets')
ext['spring.version'] = '4.3.0.BUILD-SNAPSHOT'
ext['jackson.version'] = '2.7.1'

configurations {
  all*.exclude group: 'postgresql', module: 'postgresql'
}

dependencies {
  compile('org.springframework.boot:spring-boot-starter-web') {
    exclude module: 'spring-boot-starter-validation'
  }
  compile('org.springframework.boot:spring-boot-starter-jdbc')
  compile('org.springframework.boot:spring-boot-devtools')


  compile("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")
  compile('com.fasterxml.jackson.module:jackson-module-kotlin:2.7.1-2')

  compile('org.postgresql:postgresql:9.4.1208')
  compile('net.postgis:postgis-jdbc:2.2.0')
  compile('com.github.mayconbordin:postgis-geojson:1.1') // https://github.com/sdeleuze/postgis-geojson

  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('org.springframework.restdocs:spring-restdocs-mockmvc:1.0.1.RELEASE')
}


test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    dependsOn test
    sourceDir 'src/main/asciidoc'
}
