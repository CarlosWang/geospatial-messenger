buildscript {
  ext {
    springBootVersion = '1.4.0.M2'
    kotlinVersion = '1.0.2-eap-74'
    exposedVersion =  '0.5.0'
  }
  repositories {
    mavenCentral()
    maven { url 'http://repo.spring.io/milestone' }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap/' }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
  }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'kotlin'
apply plugin: 'spring-boot'

repositories {
  mavenCentral()
  maven { url 'http://repo.spring.io/milestone' }
  maven { url 'https://dl.bintray.com/kotlin/kotlin-eap/' }
  maven { url 'https://dl.bintray.com/kotlin/exposed' } // exposed
  maven { url 'https://dl.bintray.com/sdeleuze/maven/' } // postgis-geojson
  maven { url 'http://invesdwin.de/artifactory/invesdwin-oss'} // https://github.com/subes/invesdwin-instrument
    }

jar {
  baseName = 'geospatial-messenger'
  version = '1.0.0-SNAPSHOT'
  dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

ext['snippetsDir'] = file('build/generated-snippets')
ext['jackson.version'] = '2.7.3'

configurations {
  all*.exclude group: 'postgresql', module: 'postgresql'
}

dependencies {
  compile('org.springframework.boot:spring-boot-starter-web') {
    exclude module: 'spring-boot-starter-validation'
  }
  compile('org.springframework.boot:spring-boot-starter-jdbc')

  compile('org.springframework:spring-aspects')
  compile('de.invesdwin:invesdwin-instrument:1.0.0-SNAPSHOT')

  compile("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")
  compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
  compile("com.fasterxml.jackson.module:jackson-module-kotlin:${project.ext['jackson.version']}")

  compile("org.jetbrains.exposed:exposed:${exposedVersion}")
  compile("org.jetbrains.exposed:spring-transaction:${exposedVersion}")
  compile('org.postgresql:postgresql:9.4.1208')
  compile('net.postgis:postgis-jdbc:2.2.0')
  compile('com.github.mayconbordin:postgis-geojson:1.1') // https://github.com/sdeleuze/postgis-geojson

  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('org.springframework.restdocs:spring-restdocs-mockmvc:1.0.1.RELEASE')
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
}


test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    dependsOn test
    sourceDir 'src/main/asciidoc'
}
